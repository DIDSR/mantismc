# gnuplot script file for penEasy
# Last update:
#   2005-02-21 by JS

pause -1 "If the name of your particle track file is not 'tracks.dat' this script will abort."
pause -1 "In this case, edit this script and replace 'tracks.dat' by the actual name."

set style line 1 linetype -1 linewidth 1 pointtype 8 pointsize 1

set title "Number of particles of each type in the track data file"
unset xlabel
set xtics ("electrons" 1, "photons" 2, "positrons" 3)
set ylabel "No. of particles"
set boxwidth 5 relative
set style fill solid 0.5
plot [0:4] [0:] "tracks.dat" using 1:(1) notitle smooth frequency with boxes linestyle 1
pause -1 "Press OK"


###################
# Particle tracks #
###################

reset
set size ratio -1
set xrange [-5e-4:5e-4]
set yrange [-5e-4:5e-4]
set zrange [-20e-4:100e-4]

set title "Electron tracks"
set xlabel "x (cm)"
set ylabel "y (cm)"
set zlabel "z (cm)"
splot "" using ($1==1?$4:0):($1==1?$5:0):($1==1?$6:0) notitle with lines
pause -1 "Press OK"


set xrange [-5e-3:5e-3]
set yrange [-5e-3:5e-3]
set title "Photon tracks"
set xlabel "x (cm)"
set ylabel "y (cm)"
set zlabel "z (cm)"
splot "" using ($1==2?$4:0):($1==2?$5:0):($1==2?$6:0) notitle with lines
pause -1 "Press OK"

set title "Positron tracks"
set xlabel "x (cm)"
set ylabel "y (cm)"
set zlabel "z (cm)"
splot "" using ($1==3?$4:0):($1==3?$5:0):($1==3?$6:0) notitle with lines
pause -1 "Press OK"




